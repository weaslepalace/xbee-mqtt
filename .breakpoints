break main
break notifyCallback
  commands
    silent
    printf "Notify: Socket %d  Frame 0x%02X  Message 0x%02X\n", sockid, frame_type, message
    continue
  end
break receiveCallback
  commands
    silent
    printf "Receive: Socket %d  Status 0x%02X  Length 0x%02X\n", sock, status, payload_length
    set $i = 0
    while $i < payload_length
      printf " 0x%02X", ((char*)payload)[$i]
      set $i = $i + 1
    end
	printf "\n"
    continue
  end
break sendMessage
  commands
	bt
    printf "Send: Length %d\n", message_len
    set $i = 0
    while $i < message_len
      printf " 0x%02X", message[$i]
      set $i = $i + 1
    end
    printf "\n"
    continue
  end
break __trap__
  commands
    silent
    printf "Sent: %d\n", status
    continue
    end

